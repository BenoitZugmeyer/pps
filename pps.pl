#!/usr/bin/perl
use strict;
use warnings;
use LWP::Simple;
use Term::ANSIColor;
use HTML::Entities;
use HTML::PullParser;
use Getopt::Std;
use utf8;
binmode(STDOUT, ":utf8");
use threads('yield',
			'stack_size' => 64*4096,
			'exit' => 'threads_only',
			'stringify');

# constants
use constant BASEURL => scalar "http://thepiratebay.se/search";
use constant COMMENTS_COLOR => scalar 'black on_yellow';
use constant NO_SEEDERS_COLOR => scalar 'white on_red';
use constant SKULL_CROSSBONES => scalar '(â˜ )';
use constant DEFAULT_SORTING => scalar "s";
my %SORTINGS = (
	n => 1,
	N => 2,
	d => 3,
	D => 4,
	z => 5,
	Z => 6,
	s => 7,
	S => 8,
	l => 9,
	L => 10,
	u => 11,
	U => 12,
	c => 13,
	C => 14
);
my %RANK_COLORS = (
	Trusted			=> 'bold white on_magenta',
	VIP				=> 'bold white on_green',
	Helper			=> 'bold white on_blue',
	Administrator	=> 'bold black on_white',
	Moderator		=> 'bold black on_white'
);
$Getopt::Std::STANDARD_HELP_VERSION = 1;
$Term::ANSIColor::EACHLINE = "\n";

# argument handling
sub HELP_MESSAGE {
	print "Usage: $0 KEYWORDS [PARAMS]\n";
	print "       $0 [PARAMS] KEYWORDS\n";
	print "Accepted options for PARAMS are:\n";
	print "    -h : Show this message\n";
	print "    -i : Show additional information (uploader, date of upload and category).\n";
	print "    -s [SORTING]: Specify sorting method. Valid options for SORTING are:\n";
	print "        n : Sort results by name (descending order).\n";
	print "        N : Sort results by name (ascending order).\n";
	print "        d : Sort results by date of upload (descending order).\n";
	print "        D : Sort results by date of upload (ascending order).\n";
	print "        z : Sort results by size (descending order).\n";
	print "        Z : Sort results by size (ascending order).\n";
	print "        s : Sort results by number of seeders (descending order).\n";
	print "        S : Sort results by number of seeders (ascending order).\n";
	print "        l : Sort results by number of leechers (descending order).\n";
	print "        L : Sort results by number of leechers (ascending order).\n";
	print "        u : Sort results by uploader (descending order).\n";
	print "        U : Sort results by uploader (ascending order).\n";
	print "        c : Sort results by category (descending order).\n";
	print "        C : Sort results by category (ascending order).\n";
	print "        Default sorting option is descending number of seeders (s).\n";
	print "    -v : Show version number\n";
	print "Remember that if your keywords contain spaces you must surround them in quotes.\n"
}
sub VERSION_MESSAGE { print "PPS (Perl Pirate Search) version 1.1\n";}

if(@ARGV == 0) { VERSION_MESSAGE(); HELP_MESSAGE(); exit 1; }
my $kw_first = substr($ARGV[0], 0, 1) ne '-';
my $keyword;
if($kw_first) {
	$keyword = shift;
} else {
	$keyword = $ARGV[$#ARGV]
}
if(! $kw_first && (substr($ARGV[-1], 0, 1) eq '-' || $ARGV[-2] eq '-s')) {
	HELP_MESSAGE();
	exit 1;
}
my %args;
getopts("hivs:", \%args);
VERSION_MESSAGE() if($args{v});
HELP_MESSAGE() if($args{h});
my $sorting = $SORTINGS{$args{s} // +DEFAULT_SORTING} // $SORTINGS{+DEFAULT_SORTING};

# global vars
my @results_cache;
my $last_page;

#functions
sub get_text {
	#$_[1] = find the next text string that matches this regex. If not given, find any text string.
	my $pattern = qr/$_[1]/ if($_[1]);
	while(my $r = $_[0]->get_token()) {
		if($r and $$r[0] eq 'T' and $$r[1] =~ /^\S/) {
			$$r[1] =~ /^\s*(.*?)\s*$/;
			my $blah = $1;
			return $blah if(not $_[1] or $blah =~ /$pattern/);
		}
	}
	return undef;
}

sub get_stuff {
	my ($magnet, $comments, $rank);
	my $r;
	while($r = $_[0]->get_token() and ($$r[0] ne 'S' or $$r[1] ne 'a')) {}
	$magnet = $$r[2]{'href'};
	$r = $_[0]->get_token(); #magnet icon
	$r = $_[0]->get_token(); $r = $_[0]->get_token() if($$r[1] ne 'img');
	$comments = $1 if($$r[2]{'alt'} and $$r[2]{'alt'} =~ /^This torrent has (\d+)/);
	if($comments) {
		$r = $_[0]->get_token(); $r = $_[0]->get_token() if($$r[1] ne 'img');
	}
	if($$r[2]{'alt'} and $$r[2]{'alt'} =~ /cover/) {
		$r = $_[0]->get_token(); $r = $_[0]->get_token() if($$r[1] ne 'img');
	}
	$rank = $$r[2]{'alt'} if($$r[2]{'alt'} and grep($$r[2]{'alt'}, qw(Trusted VIP Admin Moderator)));
	return ($magnet, $comments // 0, $rank);
}

sub download_page {
	(my $keyword, my $page_num, my $sorting) = @_;
	$SIG{'KILL'} = sub { threads->exit };
	my $url = BASEURL . "/$keyword/$page_num/$sorting/0";
	my $page = get "$url" or die "Error getting web: $url";
	my @results_page;
	my $p = HTML::PullParser->new(
		doc   => $page,
		start => '"S", tag, attr',
		text  => '"T", text',
	);
	$p->unbroken_text(1);
	if(not $last_page) {
		my $r = get_text($p, '^Search results');
		$r =~ /approx (\d+)/;
		if($1) { $last_page = int(int($1) / int(30)); }
		else {$last_page = -1; return \@results_page; }
	}
	get_text($p, '^LE$');
	while($_ = get_text($p) and $_ ne 'Login |') {
		my %results_item;
		$results_item{'category'} = $_;
		$results_item{'sub_category'} = get_text($p);
		chop($results_item{'sub_category'});
		$results_item{'title'} = decode_entities(get_text($p));
		($results_item{'magnet'}, $results_item{'comments'}, $results_item{'rank'}) = get_stuff($p);
		get_text($p) =~ /^\w+ ([^&]+)\D+([^,]+), \S+ ([^&]+)[^;]+;([^,]+)/;
		($results_item{'date'}, $results_item{'date_year_time'}) = ($1, $2);
		($results_item{'size_value'}, $results_item{'size_unit'}) = ($3, $4);
		$results_item{'uploader'} = get_text($p);
		$results_item{'seeders'} = get_text($p);
		$results_item{'leechers'} = get_text($p);
		push @results_page, \%results_item;
	}
	return \@results_page;
}

sub print_page {
	my @results_page = @{$_[0]};
	my $index = $_[1];
	foreach(@results_page) {
		my %results_item = %{$_};
		print "$index: ";
		print " " if($index < 10);
		print colored ("$results_item{'title'}", 'bold');
		if(! $args{i} && $results_item{'rank'}) {
			print " ";
			print color $RANK_COLORS{$results_item{'rank'}};
			print +SKULL_CROSSBONES;
			print color 'reset';
		}
		print " ($results_item{'size_value'} $results_item{'size_unit'}) ";
		print color +NO_SEEDERS_COLOR if($results_item{'seeders'} == 0);
		print "($results_item{'seeders'}/$results_item{'leechers'})";
		print color 'reset' if($results_item{'seeders'} == 0);
		if($results_item{'comments'} > 0) {
			print " ";
			print colored ("($results_item{'comments'})", +COMMENTS_COLOR);
		}
		print "\n";

		if($args{i}) {
			print "    Uploaded by ";
			print color $RANK_COLORS{$results_item{'rank'}} if($results_item{'rank'});
			print "$results_item{'uploader'}";
			print color 'reset';
			print " on date" if($results_item{'date'} =~ /\d\d-\d\d/);
			print " $results_item{'date'}";
			if($results_item{'date_year_time'} =~ /\d\d:\d\d/) {
				print " at ";
			} else {
				print "-";
			}
			print "$results_item{'date_year_time'}";
			print " to category $results_item{'category'}/$results_item{'sub_category'}.\n";
		}
		++ $index;
	}
}

sub do_page {
	my $page_num = $_[0];
	my @results_page;
	if($results_cache[$page_num]) {
		@results_page = @{$results_cache[$page_num]};
	} else {
		print "Downloading data...\n";
		@results_page = @{download_page($keyword, $page_num, $sorting)};
		$results_cache[$page_num] = \@results_page;
	}
	print_page(\@results_page, 1 + 30 * $page_num);
	print "Enter 'n' for next page. Enter 'p' for previous page.\n";
	print "Enter 'w' to wipe the cache and reload the page. Enter 'q' to quit.\n";
	print "Enter the numbers of the files you would like to download: ";

	my $thread_previous =	threads->create('download_page', $keyword, $page_num - 1, $sorting) if($page_num > 1 && ! $results_cache[$page_num - 1]);
	my $thread_next=		threads->create('download_page', $keyword, $page_num + 1, $sorting) if($page_num < $last_page && ! $results_cache[$page_num + 1]);

	while(1) {
		my @selected = split /\s+/, <STDIN>;
		foreach(@selected) {
			if($_ =~ /\d+/) {
				if(not @results_page) {
					print "No torrents found.\n";
				} elsif($_ > 0 && ${$results_cache[int($_ / 30)]}[$_ - 1 - 30 * $page_num]) {
					print "Downloading ${$results_cache[int($_ / 30)]}[$_ - 1 - 30 * $page_num]{'title'}.\n";
					system("xdg-open ${$results_cache[$page_num]}[$_ - 1 - 30 * $page_num]{'magnet'} >/dev/null 2>&1");
				} else {
					print "Invalid index.\n";
				}
			} elsif($_ eq "n") {
				if($page_num < $last_page) {
					$thread_previous->kill('KILL')->detach if(defined $thread_previous);
					$results_cache[$page_num + 1] = $thread_next->join() if(defined $thread_next);
					do_page($page_num + 1);
				} else {
					print "There are no more pages.\n";
				}
				last;
			} elsif($_ eq "p") {
				if($page_num > 0) {
					$thread_next->kill('KILL')->detach if(defined $thread_next);
					$results_cache[$page_num - 1] = $thread_previous->join() if(defined $thread_previous);
					do_page($page_num - 1);
				} else {
					print "There are no previous pages.\n";
				}
				last;
			} elsif($_ eq "q") {
				$thread_previous->kill('KILL')->detach if (defined $thread_previous);
				$thread_next->kill('KILL')->detach if (defined $thread_next);
				exit 0;
			} elsif($_ eq "w") {
				@results_cache = ();
				do_page($page_num);
				last;
			}
		}
	}
}

# main rutine
do_page 0
